/* --  Supervisor mode interrupt management functions -- */

    .extern last_cause_any
    .extern last_tval_any
    .extern last_epc_any

.globl handle_trap
handle_trap:
    csrr  t0, scause
    csrr  t1, stval
    csrr  t2, sepc
    la    t3, last_cause_any
    sd    t0, 0(t3)
    la    t3, last_tval_any
    sd    t1, 0(t3)
    la    t3, last_epc_any
    sd    t2, 0(t3)

    addi  sp, sp, -16
    sd    t0, 8(sp)
    sd    t1, 0(sp)
    csrr  t0, scause
    bltz  t0, .L_is_interrupt
    ld    t1, 0(sp)
    ld    t0, 8(sp)
    addi  sp, sp, 16
    j     handle_exception

.L_is_interrupt:
    andi  t0, t0, 0x1FF
    li    t1, 5
    beq   t0, t1, .L_clk
    li    t1, 1
    beq   t0, t1, .L_sys
    ld    t1, 0(sp)
    ld    t0, 8(sp)
    addi  sp, sp, 16
    j     handle_plic

.L_clk:
    ld    t1, 0(sp)
    ld    t0, 8(sp)
    addi  sp, sp, 16
    j     handle_clk

.L_sys:
    csrci sip, 0x2
    ld    t1, 0(sp)
    ld    t0, 8(sp)
    addi  sp, sp, 16
    j     handle_syscall

.globl init_interrupts
init_interrupts:            # --
	la t0, handle_trap      #  |  Setup the interrupt handler for any interrupts that are delegated
	csrw stvec, t0          #  |  to Supervisor mode.  (Must be called from Supervisor mode)
	ret                     # --

.globl acknowledge_interrupt
acknowledge_interrupt:        # --
	csrrc zero, sip, a0       #  |  Untrigger a Supervisor interrupt.  This is necessary for software
    ret                       #  |  timers since the STIP bit does not automatically clear.
                              # --

.globl signum
.align 4
signum:
	.dword 0
	
.globl raise_syscall
raise_syscall:
	la t0, signum
	sd a0, 0(t0)
	csrwi sip, 0x2
	ret

/* --  Machine mode interrupt management functions -- */

.globl set_interrupt          # --
set_interrupt:                #  |  Enables a class of interrupts to trigger the corresponding handler.
	csrrs a0, mie, a0     #  |  Disabled through the general mstatus/sstatus bits.
	ret                   # --

/*
 * The '__m_trap_vector' label is a table of functions that handle various special interrupts
 * and exceptions generated by the hardware. The '__m_trap_vector' label is set through the 'mtvec'
 * register.  (see bootstrap.s)
*/

.global save_and_handle_m
save_and_handle_m:
    # snapshot M stuff and stuff into global variables
    csrr  t0, mcause
    csrr  t1, mtval
    csrr  t2, mepc
    la    t3, last_cause_any
    sd    t0, 0(t3)
    la    t3, last_tval_any
    sd    t1, 0(t3)
    la    t3, last_epc_any
    sd    t2, 0(t3)

    csrr  t3, mscratch
    mv    sp, t3
    j     handle_exception

__noop:	mret

.globl __m_trap_vector
.align 8
__m_trap_vector:            # Interrupt table index | Cause
.org __m_trap_vector + 0*4  #-----------------------+---------------------------------------
 	j save_and_handle_m  #  0                    | SOFTWARE interrupt [User] or Exception
.org __m_trap_vector + 1*4  #-----------------------+---------------------------------------
	j __noop            #  1                    | SOFTWARE interrupt [Supervisor]
.org __m_trap_vector + 2*4  #-----------------------+---------------------------------------
	j __noop            #  2                    | ------ /reserved/
.org __m_trap_vector + 3*4  #-----------------------+---------------------------------------
	j __noop            #  3                    | SOFTWARE interrupt [Machine]
.org __m_trap_vector + 4*4  #-----------------------+---------------------------------------
	j __noop            #  4                    | TIMER interrupt    [User]
.org __m_trap_vector + 5*4  #-----------------------+---------------------------------------
	j __noop            #  5                    | TIMER interrupt    [Supervisor]
.org __m_trap_vector + 6*4  #-----------------------+---------------------------------------
	j __noop            #  6                    | ------ /reserved/
.org __m_trap_vector + 7*4  #-----------------------+---------------------------------------
	j delegate_clk      #  7                    | TIMER interrupt    [Machine]
.org __m_trap_vector + 8*4  #-----------------------+---------------------------------------
	j __noop            #  8                    | EXTERNAL interrupt [User]
.org __m_trap_vector + 9*4  #-----------------------+---------------------------------------
	j __noop            #  9                    | EXTERNAL interrupt [Supervisor]
.org __m_trap_vector + 10*4 #-----------------------+---------------------------------------
	j __noop            # 10                    | ----- /reserved/
.org __m_trap_vector + 11*4 #-----------------------+---------------------------------------
	j __noop            # 11                    | EXTERNAL interrupt [Machine]
                            #-----------------------+---------------------------------------
